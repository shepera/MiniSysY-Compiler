CompUnit     -> [CompUnit] (Decl | FuncDef)
Decl         -> ConstDecl | VarDecl
ConstDecl    -> 'const' BType ConstDef { ',' ConstDef } ';'
BType        -> 'int'
ConstDef     -> Ident { '[' ConstExp ']' } '=' ConstInitVal
ConstInitVal -> ConstExp 
                | '{' [ ConstInitVal { ',' ConstInitVal } ] '}'
VarDecl      -> BType VarDef { ',' VarDef } ';'
VarDef       -> Ident { '[' ConstExp ']' }
                | Ident { '[' ConstExp ']' } '=' InitVal
InitVal      -> Exp 
                | '{' [ InitVal { ',' InitVal } ] '}'
FuncDef      -> FuncType Ident '(' [FuncFParams] ')' Block
FuncType     -> 'void' | 'int'  
FuncFParams  -> FuncFParam { ',' FuncFParam }
FuncFParam   -> BType Ident ['[' ']' { '[' Exp ']' }]
Block        -> '{' { BlockItem } '}'
BlockItem    -> Decl | Stmt

Stmt         -> Assign
                | Exp-Stmt
                | Block
                | If
                | While
                | Break
                | Continue
                | Return
Assign       -> LVal '=' Exp ';' 
Exp-Stmt     -> [Exp] ';' 
If           -> 'if' '(' Cond ')' Stmt [ 'else' Stmt ]
While        -> 'while' '(' Cond ')' Stmt
Break        -> 'break' ';' 
Continue     -> 'continue' ';'
Return       -> 'return' [Exp] ';'

Exp          -> AddExp
Cond         -> LOrExp
LVal         -> Ident {'[' Exp ']'}
PrimaryExp   -> '(' Exp ')' | LVal | Number
UnaryExp     -> PrimaryExp 
                | Ident '(' [FuncRParams] ')'
                | UnaryOp UnaryExp
UnaryOp      -> '+' | '-' | '!'  // 注：保证 '!' 仅出现在 Cond 中
FuncRParams  -> Exp { ',' Exp }
MulExp       -> UnaryExp 
                | MulExp ('*' | '/' | '%') UnaryExp
AddExp       -> MulExp 
                | AddExp ('+' | '−') MulExp
RelExp       -> AddExp 
                | RelExp ('<' | '>' | '<=' | '>=') AddExp
EqExp        -> RelExp 
                | EqExp ('==' | '!=') RelExp
LAndExp      -> EqExp 
                | LAndExp '&&' EqExp
LOrExp       -> LAndExp 
                | LOrExp '||' LAndExp
ConstExp     -> AddExp  // 在语义上额外约束这里的 AddExp 必须是一个可以在编译期求出值的常量